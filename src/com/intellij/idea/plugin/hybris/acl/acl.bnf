/*
 * This file is part of "SAP Commerce Developers Toolset" plugin for IntelliJ IDEA.
 * Copyright (C) 2019-2025 EPAM Systems <hybrisideaplugin@epam.com> and contributors
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

/*

    See {@link de.hybris.platform.jalo.security.ImportExportUserRightsHelper} for Language details.

 */
{
  parserClass="com.intellij.idea.plugin.hybris.acl.AclParser"
  parserUtilClass="com.intellij.idea.plugin.hybris.acl.utils.AclParserUtils"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Acl"
  psiImplClassSuffix="Impl"
  psiPackage="com.intellij.idea.plugin.hybris.acl.psi"
  psiImplPackage="com.intellij.idea.plugin.hybris.acl.psi.impl"
  psiImplUtilClass="com.intellij.idea.plugin.hybris.acl.psi.AclPsiUtil"

  elementTypeHolderClass="com.intellij.idea.plugin.hybris.acl.psi.AclTypes"
  elementTypeClass="com.intellij.idea.plugin.hybris.acl.psi.AclElementType"
  tokenTypeClass="com.intellij.idea.plugin.hybris.acl.psi.AclTokenType"

  classHeader="generatedFilesHeader.txt"
  implements("user\_rights\_header\_parameter.*")="com.intellij.idea.plugin.hybris.acl.psi.AclUserRightsHeaderParameter"

  generate=[java='21']
}

aclFile ::= root*

private root ::= (CRLF | ( !<<eof>> root_group (CRLF | <<eof>> )))
{recoverWhile = recover_root}

private root_group ::= (
      user_rights
    | comment
    | (string (';')?)
)
{recoverWhile = not_line_break}

private recover_root ::= !(
     CRLF
  |  user_rights_start
  |  LINE_COMMENT
)

user_rights ::= user_rights_start (PARAMETERS_SEPARATOR)* CRLF+ user_rights_body user_rights_end {
    pin=1
    recoverWhile=recover_root
}

user_rights_start ::= START_USERRIGHTS
user_rights_end ::= END_USERRIGHTS (PARAMETERS_SEPARATOR)*

private user_rights_body ::= (user_rights_header_line CRLF+) (user_rights_value_line CRLF+)*
// this mapping may enable multiple Header lines
// ((user_rights_header_line CRLF+) (user_rights_value_line CRLF+)*)*
{
    recoverWhile=user_rights_body_recover
}
private user_rights_body_recover ::= !(user_rights_end)

user_rights_header_line ::= user_rights_header_parameter_type
    user_rights_header_parameter_uid
    user_rights_header_parameter_member_of_groups
    user_rights_header_parameter_password
    user_rights_header_parameter_target
    user_rights_header_parameter_permission*  {
    pin=1
    recoverWhile=not_line_break
}

user_rights_header_parameter_type ::= HEADER_TYPE
user_rights_header_parameter_uid ::= PARAMETERS_SEPARATOR HEADER_UID { pin=1 recoverWhile=recover_header_line }
user_rights_header_parameter_member_of_groups ::= PARAMETERS_SEPARATOR HEADER_MEMBEROFGROUPS { pin=1 recoverWhile=recover_header_line }
user_rights_header_parameter_password ::= PARAMETERS_SEPARATOR HEADER_PASSWORD { pin=1 recoverWhile=recover_header_line }
user_rights_header_parameter_target ::= PARAMETERS_SEPARATOR HEADER_TARGET { pin=1 recoverWhile=recover_header_line }
user_rights_header_parameter_permission ::= PARAMETERS_SEPARATOR (
    HEADER_READ
    | HEADER_CHANGE
    | HEADER_CREATE
    | HEADER_REMOVE
    | HEADER_CHANGE_PERM
) { pin=1 recoverWhile=recover_header_line }

user_rights_value_line ::= user_rights_value_type
    user_rights_value_uid
    user_rights_value_member_of_groups
    user_rights_value_password?
    user_rights_value_target
    user_rights_value_permission*  {
    recoverWhile=not_line_break
}

user_rights_value_type ::= FIELD_VALUE? { recoverWhile=recover_value_line }
user_rights_value_uid ::= FIELD_VALUE_SEPARATOR FIELD_VALUE? {
    pin=1
    recoverWhile=recover_value_line
}
user_rights_value_member_of_groups ::= FIELD_VALUE_SEPARATOR FIELD_VALUE? (COMMA FIELD_VALUE)* {
    pin=1
    recoverWhile=recover_value_line
}
user_rights_value_password ::= FIELD_VALUE_SEPARATOR FIELD_VALUE? {
    pin=1
    recoverWhile=recover_value_line
}
user_rights_value_target ::= FIELD_VALUE_SEPARATOR FIELD_VALUE? (DOT FIELD_VALUE)? {
    pin=1
    recoverWhile=recover_value_line
}
user_rights_value_permission ::= FIELD_VALUE_SEPARATOR (PERMISSION_DENIED | PERMISSION_ALLOWED | PERMISSION_INHERITED)? {
    pin=1
    recoverWhile=recover_value_line
}

comment ::= LINE_COMMENT

private not_line_break ::= !(CRLF)
private recover_header_line ::= !(CRLF | PARAMETERS_SEPARATOR)
private recover_value_line ::= !(CRLF | FIELD_VALUE_SEPARATOR)
